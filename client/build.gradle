buildscript {
    ext.kotlin_version = '1.2.10'
    ext.requirejs_version = '2.3.5'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'htwdd/chessgame'
version '1.0.0'

apply plugin: 'kotlin2js'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile "org.webjars:requirejs:$requirejs_version"
}

compileKotlin2Js {
    kotlinOptions.outputFile = "${projectDir}/dist/js/client.js"
    kotlinOptions.moduleKind = "amd"
    kotlinOptions.sourceMap = true
}

build.doLast {
    configurations.compile.each { File file ->
        copy {
            includeEmptyDirs = false

            from zipTree(file.absolutePath)
            into "${projectDir}/dist/js"
            include { fileTreeElement ->
                def path = fileTreeElement.path
                println(path)
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
            }
            eachFile { fcp ->
                fcp.path = fcp.path.replaceFirst(/.*\//, '')
            }
        }
    }

    copy {
        includeEmptyDirs = false
        from new File("src/main/resources")
        into "dist"
    }
}